Import('env')


c_env = env.Clone()
if env['platform'].startswith('windows'):
    c_env.AppendUnique(LIBS=["python37"])
else:  # x11&osx
    c_env.AppendUnique(LIBS=["python3.7m"])
    c_env.AppendUnique(LINKFLAGS=["-Wl,-rpath,'$$ORIGIN/lib'"])
    c_env.AppendUnique(CFLAGS=['-Werror-implicit-function-declaration'])
c_env.Depends('pythonscript.c', env['cpython_build'])
libpythonscript, *_ = c_env.SharedLibrary('pythonscript', [
    'pythonscript.c',
])
env.Install(
    "$DIST_PLATFORM",
    libpythonscript
)


# Cython modules depend on libpythonscript
env.AppendUnique(LIBPATH=[Dir(".")])
env.AppendUnique(CYTHON_COMPILE_DEPS=[libpythonscript])


SConscript([
    "godot/SConscript"
])


*mods, _ = env.CythonModule(['_godot', '_godot_api.h'], [
    '_godot.pyx',
    '_godot_editor.pxi',
    '_godot_instance.pxi',
    '_godot_profiling.pxi',
    '_godot_script.pxi',
])
env.Install(
    "$DIST_SITE_PACKAGES",
    mods
)
